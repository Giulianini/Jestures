plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id 'com.jfrog.artifactory' version '4.9.6'
    id 'java'
    id 'java-library'
    id 'application'
    id 'maven'
    id 'maven-publish'
    id 'signing'
    id 'pmd'
    id 'findbugs'
    id 'checkstyle'
    id 'project-report'
    id 'jacoco'
}

mainClassName = 'recorder.controller.Main'


group = project_group
archivesBaseName = project_artifact
version = project.project_version

//TASKS
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes 'Main-Class': 'recorder.controller.Main'
    }
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation "org.apache.commons:commons-math3:${project.commonMathVersion}"
    implementation "log4j:log4j:${project.log4jVersion}"
    implementation "com.google.code.gson:gson:${project.gsonVersion}"
    implementation "com.jfoenix:jfoenix:${project.jfoenixVersion}"
    implementation "org.kordamp.ikonli:ikonli-core:${project.ikonli_coreVersion}"
    implementation "org.kordamp.ikonli:ikonli-javafx:${project.ikonli_javafxVersion}"
    implementation "org.kordamp.ikonli:ikonli-material-pack:${project.ikonli_material_packVersion}"
    implementation "eu.hansolo.enzo:Enzo:${project.enzoVersion}"
    testImplementation("org.junit.jupiter:junit-jupiter:${project.junitVersion}")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:${project.junitVersion}")
    implementation project(':Jestures')
}

test {
    useJUnitPlatform()
}

//CODE ANALYSIS
check.dependsOn jacocoTestCoverageVerification
projectReport.finalizedBy jacocoTestReport

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.1
            }
        }
    }
}

checkstyle {
    toolVersion = '8.0'
    configFile = file('config/checkstyle/checkstyle.xml')
}

findbugs {
    ignoreFailures = true
    sourceSets = [sourceSets.main]
    effort = "max"
}

pmdMain {
    ignoreFailures = true
    //ruleSets = ["category/java/errorprone.xml"]
}

//BINTRAY
bintray {
    user = project.hasProperty('bintrayUsername') ? project.property('bintrayUsername') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_KEY')


    publications = ['maven']

    dryRun = false //[Default: false] Whether to run this as dry-run, without deploying
    publish = true //[Default: false] Whether version should be auto published after an upload    
    override = false //[Default: false] Whether to override version artifacts already published

    pkg {
        repo = project.bintray_repo_name
        name = project.project_name
        desc = project.project_description
        licenses = [project.project_license_tag]
        labels = ['Jestures', 'sensor', 'kinect', 'tracking', 'Recognition', 'Gesture']
        publicDownloadNumbers = true
        websiteUrl = "https://github.com/$project.your_github_name/$project.project_name"
        issueTrackerUrl = "https://github.com/$project.your_github_name/$project.project_name/issues"
        vcsUrl = "https://github.com/$your_github_name/${project.project_name}.git"
        githubRepo = "$your_github_name/$project.project_name"
        githubReleaseNotesFile = 'Releasenotes.md'
        version {
            name = project.project_version
            vcsTag = project.project_version
            desc = project.project_version
            released = new Date()
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = false //[Default: true] Determines whether to sync the version to Maven Central.
                //user = 'userToken' //OSS user token: mandatory
                //password = 'paasword' //OSS user password: mandatory
                //close = '1'
            }
        }
    }
}

// to publish SNAPSHOTs to http://oss.jfrog.org/oss-snapshot-local/ 
// and !SNAPSHOTs to http://oss.jfrog.org/oss-release-local/
artifactory {
    contextUrl = 'https://oss.jfrog.org'
    resolve {
        repository {
            repoKey = 'libs-release'
        }
    }
    publish {
        repository {
            // The Artifactory repository key to publish to
            // when using oss.jfrog.org the credentials are from Bintray.
            if (project.version.endsWith("-SNAPSHOT")) {
                repoKey = 'oss-snapshot-local'
            } else {
                repoKey = 'oss-release-local'
            }
            username = System.getenv('BINTRAY_USER')
            password = System.getenv('BINTRAY_KEY')
        }
        defaults {
            publications 'maven'
            properties = ['bintray.repo': 'giulianini/maven', 'bintray.package': 'giulianini:giulianini', 'bintray.version': project.project_version.toString()]
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId project.project_group
            artifactId project.project_artifact
            version project.project_version

            artifact sourcesJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }

            pom {
                packaging 'jar'
                withXml {
                    asNode().children().last() + {
                        def builder = delegate

                        // maven central publishing mandatories
                        builder.name project.project_name
                        builder.description project.project_description
                        builder.url "https://github.com/$project.your_github_name/$project.project_name"

                        builder.licenses {
                            builder.license {
                                builder.name project.project_license
                                builder.url project.project_license_url
                                builder.distribution 'repo'
                            }
                        }

                        builder.scm {
                            builder.url "scm:svn:http://github.com/$project.your_github_name/$project.project_name"
                            builder.connection "scm:svn:http://github.com/$your_github_name/${project.project_name}.git"
                            builder.developerConnection "scm:svn:ssh://github.com/$your_github_name/${project.project_name}.git"
                        }

                        builder.developers {
                            builder.developer {
                                builder.name 'Luca Giulianini'
                                builder.email 'luca.giulianini@studio.unibo.it'
                            }
                        }

                        // maven central publishing optionals
                        builder.issueManagement {
                            builder.system 'github'
                            builder.url "https://github.com/$project.your_github_name/$project.project_name/issues"
                        }

                        builder.ciManagement {
                            builder.system 'Travis CI'
                            builder.url "https://travis-ci.org/$project.your_github_name/$project.project_name"
                        }
                    }
                }
            }
        }
    }
}

publishing {
    if (!version.toString().endsWith('-SNAPSHOT'))
        publish.dependsOn bintrayUpload
    else if (version.toString().endsWith('-SNAPSHOT'))
        publish.dependsOn artifactoryPublish
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

artifacts {
    archives javadocJar, sourcesJar, jar
}

task printVersion() {
    println(project.project_version)
}




